{
  lib,
  pkgs,
  ...
}:
let
  monitorConfig = import ../monitor-config.nix;
  devices = builtins.attrNames monitorConfig.defaultLayoutsByHost;
  configFile = pkgs.writeText "hyprmonitor-config.json" (
    builtins.toJSON {
      defaultLabel = "Default";
      outputPath = "~/.config/hypr/hyprmonitor.conf";
      tabletHeadless = monitorConfig.tabletHeadless;
      devices = builtins.listToAttrs (
        map (device: {
          name = device;
          value = {
            defaultSettings = monitorConfig.defaultLayoutsByHost.${device}.monitorv2;
            workspaceRules = monitorConfig.workspaceByHost.${device} or [ ];
            profiles = monitorConfig.hyprmonitorProfilesByHost.${device} or [ ];
          };
        }) devices
      );
    }
  );
  rawScript = pkgs.writers.writePython3Bin "hyprmonitor" {
    libraries = with pkgs.python3Packages; [
      click
    ];
  } (builtins.readFile ./main.py);
  script = pkgs.symlinkJoin {
    name = "hyprmonitor";
    paths = [ rawScript ];
    nativeBuildInputs = [ pkgs.makeWrapper ];
    postBuild = ''
      wrapProgram "$out/bin/hyprmonitor" \
        --set HYPRMONITOR_CONFIG_PATH ${configFile}
    '';
  };
in
{
  home.packages = [ script ];

  home.activation.ensureHyprmonitorConf =
    lib.hm.dag.entryAfter [ "writeBoundary" ] ''
      conf="$HOME/.config/hypr/hyprmonitor.conf"
      mkdir -p "$(dirname "$conf")"
      if [ ! -f "$conf" ]; then
        cat > "$conf" <<'EOF'
# Generated by hyprmonitor
# This file is rewritten by hyprmonitor; manual edits may be lost.

EOF
      fi
    '';
}
