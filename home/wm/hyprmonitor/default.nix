{
  config,
  lib,
  pkgs,
  ...
}:
let
  cfg = config.wm.hyprmonitor;
  monitorConfig = import ./monitor-config.nix;
  devices = builtins.attrNames monitorConfig.defaultLayoutsByHost;
  configFile = pkgs.writeText "hyprmonitor-config.json" (
    builtins.toJSON {
      defaultLabel = "Default";
      outputPath = cfg.configPath;
      tabletHeadless = monitorConfig.tabletHeadless;
      devices = builtins.listToAttrs (
        map (device: {
          name = device;
          value = {
            defaultSettings = monitorConfig.defaultLayoutsByHost.${device}.monitorv2;
            workspaceRules = monitorConfig.workspaceByHost.${device} or [ ];
            profiles = monitorConfig.hyprmonitorProfilesByHost.${device} or [ ];
          };
        }) devices
      );
    }
  );
  rawScript = pkgs.writers.writePython3Bin "hyprmonitor" {
    libraries = with pkgs.python3Packages; [
      click
    ];
  } (builtins.readFile ./main.py);
  script = pkgs.symlinkJoin {
    name = "hyprmonitor";
    paths = [ rawScript ];
    nativeBuildInputs = [ pkgs.makeWrapper ];
    meta.mainProgram = "hyprmonitor";
    postBuild = ''
      wrapProgram "$out/bin/hyprmonitor" \
        --set HYPRMONITOR_CONFIG_PATH ${configFile}
    '';
  };
  configPathForShell =
    if lib.hasPrefix "~/" cfg.configPath then
      "$HOME/${lib.removePrefix "~/" cfg.configPath}"
    else
      cfg.configPath;
in
{
  options.wm.hyprmonitor = {
    configPath = lib.mkOption {
      type = lib.types.str;
      default = "~/.config/hypr/hyprmonitor.conf";
      description = "Runtime Hyprland monitor config managed by hyprmonitor.";
    };

    package = lib.mkOption {
      type = lib.types.package;
      readOnly = true;
      description = "The packaged hyprmonitor executable derivation.";
    };
  };

  config = {
    wm.hyprmonitor.package = script;

    home.packages = [ cfg.package ];

    home.activation.ensureHyprmonitorConf = lib.hm.dag.entryAfter [ "writeBoundary" ] ''
              conf="${configPathForShell}"
              mkdir -p "$(dirname "$conf")"
              if [ ! -f "$conf" ]; then
                cat > "$conf" <<'EOF'
      # Generated by hyprmonitor
      # This file is rewritten by hyprmonitor; manual edits may be lost.

      EOF
              fi
    '';
  };
}
